#!/usr/bin/bpftrace
/* SPDX-License-Identifier: GPL-2.0+
 *
 * Catching high network IRQ-to-softirq latency by measuing time
 * between softirq "raise" until softirq function is called.  Limited
 * to network via softirq vector NET_RX_SOFTIRQ (3).
 *
 * This script was used for detecting latency issues described in blog:
 *  https://github.blog/2019-11-21-debugging-network-stalls-on-kubernetes/
 *
 * Output examples from this script can be seen in:
 *  https://bugzilla.redhat.com/show_bug.cgi?id=1795049#c8
 *
 * 26-Jan-2020	Jesper Dangaard Brouer	Created this
 * 11-Dec-2023  Jesper Dangaard Brouer	Make safe for production usage
 */
BEGIN
{
	/* Cmdline arg#1: latency threshold input in usec */
	@threshold_usecs = $1 ? $1: 5000;
	/* Cmdline arg#2: CPU to measure
	 *  On prod systems (e.g with 128 cores) limit impact
	 */
	@my_cpu = $2 ? $2 : 0;
	printf("Tracing softirq latency on CPU:%d ... Hit Ctrl-C to end.\n",
	       @my_cpu);
	printf(" - Will report on latency above %d usecs (= %d ms)\n",
	       @threshold_usecs, @threshold_usecs / 1000);
}

tracepoint:irq:softirq_raise
/args->vec == 3/   /* NET_RX_SOFTIRQ = 3 */
{
	if (@my_cpu == cpu && !@start[cpu]) {
		/* Only catch first softirq_raise, to measure time
		 * until softirq_entry happens.  Other raise events
		 * could be triggered by other NICs.
		 */
		@start[cpu] = nsecs;
	}
}

tracepoint:irq:softirq_entry
/args->vec == 3/
{
	if (@my_cpu == cpu && @start[cpu] > 0) {
		$lat = (nsecs - @start[cpu]) / 1000; /* convert to usecs */
		@latency_usecs[cpu] = hist($lat);

		/* Report on events over threshold */
		if ($lat >= @threshold_usecs) {
			// @stack[cpu, $lat] = kstack; //no useful stack
			printf("High IRQ-to-softirq latency: %d usec (%d ms) on CPU:%d\n",
			       $lat, $lat / 1000, cpu);
		}
	}
	delete(@start[cpu]);
}

END
{	/* Default bpftrace will print all remaining maps at END */
	clear(@threshold_usecs);
}
